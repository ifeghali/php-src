dnl $Id$ -*- sh -*-
dnl Process this file with autoconf to produce a configure script.



divert(0)
AC_INIT(main.c)
recurse=yes
for arg in $@; do
	case $arg in
		## This is to prevent "./config.status --recheck" from
		## running configure in libzend again.
		--no-recursion) recurse=no;;
		*) ;;
	esac
done

if test "$recurse" = "yes"; then
	(cwd=`pwd`; set -x; cd $srcdir/libzend; ./configure --cache-file=$cwd/config.cache $@)
fi

dnl ## Diversion 1 is the initial checking of OS features, programs,
dnl ## libraries and so on.

dnl ## In diversion 2 we check for compile-time options to the PHP
dnl ## core and how to deal with different system dependencies.  This
dnl ## includes what regex library is used and whether debugging or short
dnl ## tags are enabled, and the default behaviour of php3.ini options.
dnl ## This is also where an SAPI interface is selected (choosing between
dnl ## Apache module, CGI etc.)

dnl ## In diversion 3 we check which extensions should be compiled.
dnl ## All of these are normally in the extension directories.

dnl ## Diversion 4 is the last one.  Here we generate files and clean up.

divert(1)

dnl ## This is where the version number is changed from now on!
AM_INIT_AUTOMAKE(php, 4.0pa1)



PHP_VERSION=$VERSION
echo "#define PHP_VERSION \"$PHP_VERSION\"" > php_version.h.new
if ! cmp php_version.h.new php_version.h >/dev/null; then
    rm -f php_version.h && mv php_version.h.new php_version.h && \
    echo 'Updated php_version.h'
else
	rm -f php_version.h.new
fi
AC_SUBST(PHP_VERSION)

AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

dnl We want this one before the checks, so the checks can modify CFLAGS.
test -z "$CFLAGS" && auto_cflags=1

dnl Checks for programs.
AC_PROG_YACC
if test "$YACC" != "bison -y"; then
    AC_MSG_WARN(You will need bison if you want to regenerate the PHP parsers.)
else
    AC_MSG_CHECKING(bison version)
    set `$YACC --version| sed -e 's/^GNU Bison version //' -e 's/\./ /'`
    if test "$1" = "1" -a "$2" -lt "25"; then
        AC_MSG_WARN(You will need bison 1.25 if you want to regenerate the PHP parsers (found $1.$2).)
    fi
    AC_MSG_RESULT($1.$2 (ok))
fi

dnl ## there has to be a better way...
dnl## OLDLIBS=$LIBS; LIBS=""
AC_PROG_CC
dnl## LIBS=$OLDLIBS

AC_PROG_RANLIB
AC_PROG_CC_C_O
AC_PATH_PROG(PERL_PATH, perl)

dnl Ugly hack to get around a problem with gcc on AIX.
if test "$CC" = "gcc" -a "$ac_cv_prog_cc_g" = "yes" -a \
   "`uname -sv`" = "AIX 4"; then
	CFLAGS=`echo $CFLAGS | sed -e 's/-g//'`
fi

dnl check for -R, etc. switch
AC_MSG_CHECKING(if compiler supports -R)
AC_CACHE_VAL(php_cv_cc_dashr,[
	SAVE_LIBS="${LIBS}"
	LIBS="-R /usr/lib ${LIBS}"
	AC_TRY_LINK([], [], php_cv_cc_dashr=yes, php_cv_cc_dashr=no)
	LIBS="${SAVE_LIBS}"])
AC_MSG_RESULT($php_cv_cc_dashr)
if test $php_cv_cc_dashr = "yes"; then
	ld_runpath_switch="-R"
	apxs_runpath_switch="-Wl,-R"
else
	AC_MSG_CHECKING([if compiler supports -Wl,-rpath,])
	AC_CACHE_VAL(php_cv_cc_rpath,[
		SAVE_LIBS="${LIBS}"
		LIBS="-Wl,-rpath,/usr/lib ${LIBS}"
		AC_TRY_LINK([], [], php_cv_cc_rpath=yes, php_cv_cc_rpath=no)
		LIBS="${SAVE_LIBS}"])
	AC_MSG_RESULT($php_cv_cc_rpath)
	if test $php_cv_cc_rpath = "yes"; then
		ld_runpath_switch="-Wl,-rpath,"
		apxs_runpath_switch="-Wl,-rpath "
	else
		dnl something innocuous
		ld_runpath_switch="-L"
		apxs_runpath_switch="-L"
	fi
fi


dnl Check compiler support for -rdynamic
AC_MSG_CHECKING(if compiler supports -rdynamic)
AC_CACHE_VAL(php_cc_rdynamic,[
	SAVE_LIBS="${LIBS}"
	LIBS="-rdynamic ${LIBS}"
	AC_TRY_LINK([], [], php_cc_rdynamic=yes, php_cc_rdynamic=no)
	LIBS="${SAVE_LIBS}"])
AC_MSG_RESULT($php_cc_rdynamic)
if test $php_cc_rdynamic = "yes"; then
	LDFLAGS="${LDFLAGS} -rdynamic"
	RDYNAMIC_LFLAGS="-rdynamic"
else
	RDYNAMIC_LFLAGS=""
fi
AC_SUBST(RDYNAMIC_LFLAGS)


dnl AC_PROG_INSTALL
AC_PATH_PROG(PROG_SENDMAIL, sendmail, /usr/lib/sendmail, $PATH /usr/bin /usr/sbin /usr/etc /etc /usr/ucblib)
if test -n "$PROG_SENDMAIL"; then
  AC_DEFINE(HAVE_SENDMAIL)
fi

dnl
dnl Check for /usr/pkg/{lib,include} which is where NetBSD puts binary
dnl and source packages.  This should be harmless on other OSs.
dnl
if test -d /usr/pkg/include -a -d /usr/pkg/lib ; then
       CFLAGS="$CFLAGS -I/usr/pkg/include"
       LDFLAGS="$LDFLAGS -L/usr/pkg/lib"
fi

INCLUDES=""
AC_SUBST(INCLUDES)

AC_PREFERRED_DB_LIB

AC_CHECK_LIB(nsl, gethostname, [
  LIBS="-lnsl $LIBS"
  AC_DEFINE(HAVE_LIBNSL) ], [])

AC_CHECK_LIB(c, socket, [:], [
 AC_CHECK_LIB(socket, socket, [
  LIBS="-lsocket $LIBS"
  AC_DEFINE(HAVE_LIBSOCKET) ], []) ])

AC_CHECK_LIB(c, gethostbyaddr, [:], [
 AC_CHECK_LIB(nsl, gethostbyaddr, [
  LIBS="-lnsl $LIBS"
  AC_DEFINE(HAVE_LIBNSL) ], []) ])

AC_CHECK_LIB(c, crypt, [:], [
 AC_CHECK_LIB(crypt, crypt, [
  LIBS="-lcrypt $LIBS"
  AC_DEFINE(HAVE_LIBCRYPT) ], []) ])

AC_CHECK_LIB(c, dlopen, [
 # fake it
 AC_DEFINE(HAVE_LIBDL) ], [
 AC_CHECK_LIB(dl, dlopen, [
  LIBS="-ldl $LIBS"
  AC_DEFINE(HAVE_LIBDL) ], []) ])

dnl The sin may be in a library which need not be specifed
dnl as well as res_search resides in libsocket
AC_CHECK_LIB(c, sin, [:], [
 AC_CHECK_LIB(m, sin) ])

dnl The res_search may be in libsocket as well, and if it is
dnl make sure to check for dn_skipname in libresolv, or if res_search
dnl is in neither of these libs, still check for dn_skipname in libresolv
AC_CHECK_LIB(socket, res_search, [
 AC_CHECK_LIB(resolv, dn_skipname)
 LIBS="$LIBS -lsocket"
 AC_DEFINE(HAVE_LIBSOCKET) ], [
 AC_CHECK_LIB(resolv, res_search, [
  LIBS="$LIBS -lresolv"
  AC_DEFINE(HAVE_LIBRESOLV) 
 ], [
  AC_CHECK_LIB(resolv, dn_skipname)
 ]) 
])

dnl Checks for header files.
AC_HEADER_STDC

dnl In QNX opendir resides in libc but dirent.h is still required
if test "`uname -s 2>/dev/null`" != "QNX"; then
	AC_HEADER_DIRENT
else
	AC_CHECK_HEADERS(dirent.h)
fi
AC_MISSING_FCLOSE_DECL
dnl QNX requires unix.h to allow functions in libunix to work properly
AC_CHECK_HEADERS(fcntl.h unistd.h crypt.h sys/file.h memory.h pwd.h grp.h sys/socket.h sys/wait.h syslog.h string.h sys/varargs.h stdarg.h sys/time.h signal.h netinet/in.h dlfcn.h limits.h sys/types.h unix.h arpa/inet.h)
if test "$DBM_LIB" = "-lgdbm"; then
	AC_CHECK_HEADER(gdbm.h, [ GDBM_INCLUDE="" ], [ 
		AC_MSG_RESULT("Try /usr/local/include/gdbm.h"); 
		AC_CHECK_HEADER(/usr/local/include/gdbm.h, [ GDBM_INCLUDE="-I/usr/local/include" ],[
			AC_MSG_RESULT("Try /opt/local/include/gdbm.h");
			AC_CHECK_HEADER(/opt/local/include/gdbm.h, [ GDBM_INCLUDE="-I/opt/local/include" ],[
				dnl if in /usr/pkg/include, do not add anything.  See above.
				AC_MSG_RESULT("Try /usr/pkg/include/gdbm.h");
				AC_CHECK_HEADER(/usr/pkg/include/gdbm.h, [ GDBM_INCLUDE="" ],[
					AC_MSG_RESULT("Giving up - You need to install gdbm.h somewhere");
					exit
				])
			])
		])	
	])
    INCLUDES="$INCLUDES $GDBM_INCLUDES"
dnl## AC_SUBST(GDBM_INCLUDE)
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_STRUCT_TM
AC_STRUCT_TIMEZONE

dnl Check for members of the stat structure
AC_STRUCT_ST_BLKSIZE
dnl AC_STRUCT_ST_BLOCKS will screw QNX because fileblocks.o does not exists
dnl The WARNING_LEVEL required because cc in QNX hates -w option without an argument
if test "`uname -s 2>/dev/null`" != "QNX"; then
	AC_STRUCT_ST_BLOCKS
else
    AC_MSG_WARN(warnings level for cc set to 0)
	WARNING_LEVEL=0
	AC_SUBST(WARNING_LEVEL)
fi
AC_STRUCT_ST_RDEV

dnl Checks for types
AC_TYPE_SIZE_T
AC_TYPE_UID_T
dnl This is required for QNX and may be some BSD derived systems
AC_CHECK_TYPE( uint, unsigned int )
AC_CHECK_TYPE( ulong, unsigned long )

dnl Checks for library functions.
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(memcpy memmove strdup strerror strcasecmp strstr flock lockf putenv tempnam usleep setlocale gettimeofday setvbuf srand48 lrand48 srandom random link symlink regcomp getlogin cuserid vsnprintf snprintf gcvt utime crypt rint unsetenv strftime setsockopt tzset)
AC_FUNC_UTIME_NULL
AC_FUNC_ALLOCA
dnl## OLDLIBS=$LIBS; LIBS=""
AC_BROKEN_SPRINTF
dnl## LIBS=$OLDLIBS
AC_REPLACE_FUNCS(getopt)

dnl AIX keeps in_addr_t in /usr/include/netinet/in.h
dnl AC_MSG_CHECKING(for in_addr_t)
AC_CACHE_VAL(ac_cv_type_$1,
[AC_EGREP_CPP(dnl
changequote(<<,>>)dnl
<<in_addr_t[^a-zA-Z_0-9]>>dnl
changequote([,]), [#include <sys/types.h>
#if STDC_HEADERS
#include <stdlib.h>
#include <stddef.h>
#endif
#ifdef HAVE_NETINET_IN_H
#include <netinet/in.h>
#endif], ac_cv_type_in_addr_t=yes, ac_cv_type_in_addr_t=no)])dnl

dnl AC_MSG_RESULT($ac_cv_type_in_addr_t)
if test $ac_cv_type_in_addr_t = no; then
  AC_DEFINE(in_addr_t, u_int)
fi

AC_CRYPT_CAP

divert(2)

AC_MSG_CHECKING(whether to use bundled regex library)
AC_ARG_WITH(system-regex,
[  --with-system-regex     Do not use the bundled regex library],
[
	REGEX_LIB=
	HSREGEX=no
 	AC_MSG_RESULT(no)
	AC_DEFINE(REGEX,0)
],[
	REGEX_LIB=regex/libregex.a
	HSREGEX=yes
 	AC_MSG_RESULT(yes)
    AC_DEFINE(HSREGEX)
	AC_DEFINE(REGEX,1)
])
AC_SUBST(REGEX_LIB)
AC_SUBST(HSREGEX)

AC_MSG_CHECKING(for Apache module support via DSO through APXS)
AC_ARG_WITH(apxs,
[  --with-apxs[=FILE]      Build shared Apache module.  FILE is the optional
                          pathname to the Apache apxs tool; defaults to "apxs".
                          (This option needs Perl installed)],
[
    if test "$withval" = "yes"; then
        withval=apxs
    fi
    APXS="$withval"
    APXS_LDFLAGS="@ORACLE_LFLAGS@ @ORACLE_LIBS@ @SYBASE_LFLAGS@ @SYBASE_LIBS@ @SYBASE_CT_LFLAGS@ @SYBASE_CT_LIBS@ @PGSQL_LFLAGS@ @PGSQL_LIBS@ @LDAP_LFLAGS@ @LDAP_LIBS@ @ZLIB_LIBS@ @PDFLIB_LIBS@ @FDFTK_LIBS@ @IFX_LFLAGS@ @IFX_LIBS@ @IBASE_LFLAGS@ @IBASE_LIBS@"
    APACHE_INCLUDE="-I`$APXS -q INCLUDEDIR`"
    BINNAME=libphp3.so
    INSTALL_IT="\$(APXS) -i -a -n php3 $BINNAME"
    CFLAGS_SHLIB=`perl -V:cccdlflags | cut -d\' -f2`
    LDFLAGS_SHLIB=`perl -V:lddlflags | cut -d\' -f2`
    LDFLAGS_SHLIB_EXPORT=`perl -V:ccdlflags | cut -d\' -f2`
    PHP_LIBS=
    STRONGHOLD=
    AC_DEFINE(APACHE)
    AC_SUBST(APXS)
    AC_SUBST(APXS_LDFLAGS)
    AC_SUBST(BINNAME)
    AC_SUBST(INSTALL_IT)
    AC_SUBST(PHP_LIBS)
    AC_DEFINE(HAVE_AP_CONFIG_H)
	AC_DEFINE(HAVE_AP_COMPAT_H)
    AC_MSG_RESULT(yes)
],[
    AC_MSG_RESULT(no)
])


APACHE_INSTALL_FILES="$srcdir/mod_php3.* $srcdir/php_version.h libphp3.module"


AC_MSG_CHECKING(for Apache module support via DSO through APACI)
AC_ARG_WITH(shared-apache,
[  --with-shared-apache[=DIR] Build shared Apache module.  DIR is the top-level 
                          Apache build directory, defaults to /usr/local/etc/httpd.
                          (This option needs Perl installed)],
[
	if test "$withval" = "yes"; then
	  # Apache's default directory
	  withval=/usr/local/etc/httpd
	fi
	if test "$withval" != "no"; then
	  if test -f $withval/src/include/httpd.h; then
	    APACHE_INCLUDE="-I$withval/src/include -I$withval/src/os/unix"
	    APACHE_TARGET=$withval/src/modules/php3
		if test ! -d $APACHE_TARGET; then
			mkdir $APACHE_TARGET
		fi
		CFLAGS_SHLIB=`perl -V:cccdlflags | cut -d\' -f2`
		LDFLAGS_SHLIB=`perl -V:lddlflags | cut -d\' -f2`
		LDFLAGS_SHLIB_EXPORT=`perl -V:ccdlflags | cut -d\' -f2`
		PHP_LIBS=
	    BINNAME=libmodphp3-so.a
	    INSTALL_IT="mkdir -p $APACHE_TARGET; cp $BINNAME $APACHE_INSTALL_FILES $APACHE_TARGET; cp apMakefile.tmpl $APACHE_TARGET/Makefile.tmpl; cp apMakefile.libdir $APACHE_TARGET/Makefile.libdir"
	    AC_DEFINE(APACHE)
	    AC_MSG_RESULT(yes - Shared Apache 1.3.x)
		STRONGHOLD=
		if test -f $withval/src/include/ap_config.h; then
		AC_DEFINE(HAVE_AP_CONFIG_H)
		fi
		if test -f $withval/src/include/ap_compat.h; then
		AC_DEFINE(HAVE_AP_COMPAT_H)
		if test ! -f $withval/src/include/ap_config_auto.h; then
    	AC_MSG_ERROR(Please run Apache's configure or src/Configure program once and try again)
		fi
		else
		if test -f $withval/src/include/compat.h; then
		AC_DEFINE(HAVE_OLD_COMPAT_H)
		fi
		fi
      else
	    AC_MSG_RESULT(no)
	    AC_MSG_ERROR(Invalid Apache directory - unable to find httpd.h under $withval/src/include)
	  fi
	fi
    INCLUDES="$INCLUDES $APACHE_INCLUDE"
    AC_SUBST(APACHE_INCLUDE)
	AC_SUBST(APACHE_TARGET)
	AC_SUBST(INSTALL_IT)
	AC_SUBST(BINNAME)
	AC_SUBST(PHP_LIBS)
],[
	AC_MSG_RESULT(no)
])



if test "$BINNAME" != "libmodphp3-so.a"; then
if test "$BINNAME" != "libphp3.so"; then
AC_MSG_CHECKING(for Apache module support)
AC_ARG_WITH(apache,
[  --with-apache[=DIR]     Build Apache module.  DIR is the top-level Apache
                          build directory, defaults to /usr/local/etc/httpd.],
[
	if test "$withval" = "yes"; then
	  # Apache's default directory
	  withval=/usr/local/etc/httpd
	fi
	if test "$withval" != "no"; then
# For Apache 1.2.x
	  if test -f $withval/src/httpd.h; then 
	    APACHE_INCLUDE=-I$withval/src
	    APACHE_TARGET=$withval/src
	    BINNAME=libphp3.a
	    INSTALL_IT="mkdir -p $APACHE_TARGET; cp $BINNAME $APACHE_INSTALL_FILES $APACHE_TARGET"
	    PHP_LIBS="-L. -lphp3"
	    AC_DEFINE(APACHE)
	    AC_MSG_RESULT(yes - Apache 1.2.x)
		STRONGHOLD=
		if test -f $withval/src/ap_config.h; then
		AC_DEFINE(HAVE_AP_CONFIG_H)
		fi
# For Apache 1.3.x
	  elif test -f $withval/src/main/httpd.h; then
	    APACHE_INCLUDE="-I$withval/src/main -I$withval/src/os/unix -I$withval/src/ap"
	    APACHE_TARGET=$withval/src/modules/php3
		if test ! -d $APACHE_TARGET; then
			mkdir $APACHE_TARGET
		fi
	    BINNAME=libmodphp3.a
	    INSTALL_IT="mkdir -p $APACHE_TARGET; cp $BINNAME $APACHE_INSTALL_FILES $APACHE_TARGET; cp $srcdir/apMakefile.tmpl $APACHE_TARGET/Makefile.tmpl; cp $srcdir/apMakefile.libdir $APACHE_TARGET/Makefile.libdir"
	    PHP_LIBS="-Lmodules/php3 -L../modules/php3 -L../../modules/php3 -lmodphp3"
	    AC_DEFINE(APACHE)
	    AC_MSG_RESULT(yes - Apache 1.3.x)
		STRONGHOLD=
		if test -f $withval/src/include/ap_config.h; then
		AC_DEFINE(HAVE_AP_CONFIG_H)
		fi
		if test -f $withval/src/include/ap_compat.h; then
		AC_DEFINE(HAVE_AP_COMPAT_H)
		if test ! -f $withval/src/include/ap_config_auto.h; then
    	AC_MSG_ERROR(Please run Apache's configure or src/Configure program once and try again)
		fi
		else
		if test -f $withval/src/include/compat.h; then
		AC_DEFINE(HAVE_OLD_COMPAT_H)
		fi
		fi
# Also for Apache 1.3.x
	  elif test -f $withval/src/include/httpd.h; then
	    APACHE_INCLUDE="-I$withval/src/include -I$withval/src/os/unix"
	    APACHE_TARGET=$withval/src/modules/php3
		if test ! -d $APACHE_TARGET; then
			mkdir $APACHE_TARGET
		fi
	    BINNAME=libmodphp3.a
	    PHP_LIBS="-Lmodules/php3 -L../modules/php3 -L../../modules/php3 -lmodphp3"
	    INSTALL_IT="mkdir -p $APACHE_TARGET; cp $BINNAME $APACHE_INSTALL_FILES $APACHE_TARGET; cp $srcdir/apMakefile.tmpl $APACHE_TARGET/Makefile.tmpl; cp $srcdir/apMakefile.libdir $APACHE_TARGET/Makefile.libdir"
	    AC_DEFINE(APACHE)
	    AC_MSG_RESULT(yes - Apache 1.3.x)
		STRONGHOLD=
		if test -f $withval/src/include/ap_config.h; then
		AC_DEFINE(HAVE_AP_CONFIG_H)
		fi
		if test -f $withval/src/include/ap_compat.h; then
		AC_DEFINE(HAVE_AP_COMPAT_H)
		if test ! -f $withval/src/include/ap_config_auto.h; then
    	AC_MSG_ERROR(Please run Apache's configure or src/Configure program once and try again)
		fi
		else
		if test -f $withval/src/include/compat.h; then
		AC_DEFINE(HAVE_OLD_COMPAT_H)
		fi
		fi
# For StrongHold 2.2
	  elif test -f $withval/apache/httpd.h; then
	    APACHE_INCLUDE=-"I$withval/apache -I$withval/ssl/include"
	    APACHE_TARGET=$withval/apache
	    BINNAME=libmodphp3.a
	    PHP_LIBS="-Lmodules/php3 -L../modules/php3 -L../../modules/php3 -lmodphp3"
	    INSTALL_IT="mkdir -p $APACHE_TARGET; cp $BINNAME $APACHE_INSTALL_FILES $APACHE_TARGET"
		STRONGHOLD=-DSTRONGHOLD=1
	    AC_DEFINE(APACHE)
		AC_MSG_RESULT(yes - StrongHold)
		if test -f $withval/apache/ap_config.h; then
		AC_DEFINE(HAVE_AP_CONFIG_H)
		fi
		if test -f $withval/src/ap_compat.h; then
		AC_DEFINE(HAVE_AP_COMPAT_H)
		if test ! -f $withval/src/include/ap_config_auto.h; then
    	AC_MSG_ERROR(Please run Apache's configure or src/Configure program once and try again)
		fi
		else
		if test -f $withval/src/compat.h; then
		AC_DEFINE(HAVE_OLD_COMPAT_H)
		fi
		fi
	  else
	    AC_MSG_RESULT(no)
	    AC_MSG_ERROR(Invalid Apache directory - unable to find httpd.h under $withval)
	  fi
	else
	  AC_MSG_RESULT(no)
	  BINNAME=php
	  INSTALL_IT="cp $BINNAME \$(bindir)"
	fi
],[
	AC_MSG_RESULT(no)
	BINNAME=php
	INSTALL_IT="cp $BINNAME \$(bindir)"
])
INCLUDES="$INCLUDES $APACHE_INCLUDE"
dnl## AC_SUBST(APACHE_INCLUDE)
AC_SUBST(APACHE_TARGET)
AC_SUBST(INSTALL_IT)
AC_SUBST(BINNAME)
AC_SUBST(STRONGHOLD)
AC_SUBST(PHP_LIBS)
fi
fi

AC_MSG_CHECKING(for mod_charset compatibility option)
AC_ARG_WITH(mod_charset,
[  --with-mod_charset      Enable transfer tables for mod_charset (Rus Apache).],
[
	AC_MSG_RESULT(yes)
    AC_DEFINE(USE_TRANSFER_TABLES)
],[
	AC_MSG_RESULT(no)
])

AC_MSG_CHECKING(for fhttpd module support)
AC_ARG_WITH(fhttpd,
[  --with-fhttpd[=DIR]     Build fhttpd module.  DIR is the fhttpd sources
                          directory, defaults to /usr/local/src/fhttpd.],
[
	if test "$withval" = "yes"; then
	  # fhttpd source directory
	  withval=/usr/local/src/fhttpd
	fi
	if test "$withval" != "no"; then
# For fhttpd 0.3.x
	  if test -f $withval/servproc.h; then 
	    FHTTPD_INCLUDE=-I$withval/
            FHTTPD_LIB=$withval/servproc.o
	    FHTTPD_TARGET=$withval/
	    BINNAME=php
	    AC_DEFINE(FHTTPD)
	    AC_MSG_RESULT(yes - fhttpd 0.3.x)
	  else
	    AC_MSG_RESULT(no)
	    AC_MSG_ERROR(Invalid fhttpd directory - unable to find servproc.h under $withval)
	  fi
	else
	  AC_MSG_RESULT(no)
	fi
],[
	AC_MSG_RESULT(no)
])
INCLUDES="$INCLUDES $FHTTPD_INCLUDE"
dnl## AC_SUBST(FHTTPD_INCLUDE)
AC_SUBST(FHTTPD_LIB)
AC_SUBST(FHTTPD_TARGET)


AC_MSG_CHECKING(whether to use a configuration file)
AC_ARG_WITH(config-file-path,
[  --with-config-file-path=PATH  Sets the path in which to look for php3.ini
                          defaults to /usr/local/lib],
[
  if test "$withval" = "yes"; then
    AC_DEFINE_UNQUOTED(CONFIGURATION_FILE_PATH, "/usr/local/lib")
    AC_DEFINE(USE_CONFIG_FILE, 1)
    AC_MSG_RESULT(yes)
  else
    if test "$withval" != "no"; then
      AC_DEFINE_UNQUOTED(CONFIGURATION_FILE_PATH, "$withval")
      AC_DEFINE(USE_CONFIG_FILE, 1)
      AC_MSG_RESULT(yes)
    else
      AC_DEFINE(CONFIGURATION_FILE_PATH, 0)
      AC_DEFINE(USE_CONFIG_FILE, 0)
      AC_MSG_RESULT(no)
    fi
  fi
],[
  AC_DEFINE_UNQUOTED(CONFIGURATION_FILE_PATH, "/usr/local/lib")
  AC_DEFINE(USE_CONFIG_FILE, 1)
  AC_MSG_RESULT(yes)
]) 


AC_MSG_CHECKING(whether to include debugging symbols)
AC_ARG_ENABLE(debug,
[  --enable-debug          Compile with debugging symbols],
[
  if test "$enableval" = "yes"; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(DEBUG,1)
    PHP_DEBUG=1
    DEBUG_CFLAGS="-g"
    test -n "$GCC" && DEBUG_CFLAGS="$DEBUG_CFLAGS -Wall"
    test -n "$GCC" && test "$USE_MAINTAINER_MODE" = "yes" && \
      DEBUG_CFLAGS="$DEBUG_CFLAGS -Wmissing-prototypes -Wstrict-prototypes -Wmissing-declarations"
  else
    AC_MSG_RESULT(no)
    AC_DEFINE(DEBUG,0)
    PHP_DEBUG=0
    DEBUG_CFLAGS=""
  fi
],[
  AC_MSG_RESULT(no)
  AC_DEFINE(DEBUG,0)
  PHP_DEBUG=0
  DEBUG_CFLAGS=""
]) 
AC_SUBST(DEBUG_CFLAGS)
AC_SUBST(PHP_DEBUG)


AC_MSG_CHECKING(whether to enable safe mode by default)
AC_ARG_ENABLE(safe-mode,
[  --enable-safe-mode      Enable safe mode by default.],
[
  if test "$enableval" = "yes"; then
    AC_DEFINE(PHP_SAFE_MODE, 1)
    AC_MSG_RESULT(yes)
  else
    AC_DEFINE(PHP_SAFE_MODE, 0)
    AC_MSG_RESULT(no)
  fi
],[
    AC_DEFINE(PHP_SAFE_MODE, 0)
    AC_MSG_RESULT(no)
]) 


AC_MSG_CHECKING(for safe mode exec dir)
AC_ARG_WITH(exec-dir,
[  --with-exec-dir[=DIR]   Only allow executables in DIR when in safe mode
                          defaults to /usr/local/php/bin],
[
  if test "$withval" != "no"; then
  	if test "$withval" = "yes"; then
		AC_DEFINE(PHP_SAFE_MODE_EXEC_DIR,"/usr/local/php/bin")
    	AC_MSG_RESULT(/usr/local/php/bin)
   	else
		AC_DEFINE_UNQUOTED(PHP_SAFE_MODE_EXEC_DIR,"$withval")
    	AC_MSG_RESULT($withval)
   	fi
  else
	AC_DEFINE(PHP_SAFE_MODE_EXEC_DIR,"/usr/local/php/bin")
   	AC_MSG_RESULT(/usr/local/php/bin)
  fi
],[
	AC_DEFINE(PHP_SAFE_MODE_EXEC_DIR,"/usr/local/php/bin")
   	AC_MSG_RESULT(/usr/local/php/bin)
])


AC_MSG_CHECKING(whether to enable track_vars variables by default)
AC_ARG_ENABLE(track-vars,
[  --enable-track-vars     Enable GET/POST/Cookie track variables by default.],
[
  if test "$enableval" = "yes"; then
    AC_DEFINE(PHP_TRACK_VARS, 1)
    AC_MSG_RESULT(yes)
  else
    AC_DEFINE(PHP_TRACK_VARS, 0)
    AC_MSG_RESULT(no)
  fi
],[
    AC_DEFINE(PHP_TRACK_VARS, 0)
    AC_MSG_RESULT(no)
]) 


AC_MSG_CHECKING(whether to enable magic quotes by default)
AC_ARG_ENABLE(magic-quotes,
[  --enable-magic-quotes   Enable magic quotes by default.],
[
  if test "$enableval" = "yes"; then
    AC_DEFINE(MAGIC_QUOTES, 1)
    AC_MSG_RESULT(yes)
  else
    AC_DEFINE(MAGIC_QUOTES, 0)
    AC_MSG_RESULT(no)
  fi
],[
    AC_DEFINE(MAGIC_QUOTES, 0)
    AC_MSG_RESULT(no)
]) 


dnl AC_MSG_CHECKING(whether to enable PHP RPC support)
dnl AC_ARG_ENABLE(php-rpc,
dnl [  --enable-php-rpc        Compile with PHP RPC support.],
dnl [
dnl   if test "$enableval" = "yes"; then
dnl 	AC_DEFINE(PHP_RPC, 1)
dnl 	AC_MSG_RESULT(yes)
dnl   else
dnl 	AC_DEFINE(PHP_RPC, 0)
dnl 	AC_MSG_RESULT(no)
dnl   fi
dnl ],[
dnl   AC_DEFINE(PHP_RPC, 0)
dnl   AC_MSG_RESULT(no)
dnl ])

if test "$BINNAME" = "php"; then
AC_MSG_CHECKING(whether to force Apache CGI redirect)
AC_ARG_ENABLE(force-cgi-redirect,
[  --enable-force-cgi-redirect Enable the security check for internal server
                              redirects.  You should use this if you are
                              running the CGI version with Apache. ],
[
  if test "$enableval" = "yes"; then
    AC_DEFINE(FORCE_CGI_REDIRECT, 1)
    AC_MSG_RESULT(yes)
    REDIRECT=1
  else
    AC_DEFINE(FORCE_CGI_REDIRECT, 0)
    AC_MSG_RESULT(no)
    REDIRECT=0
  fi
],[
  AC_DEFINE(FORCE_CGI_REDIRECT, 0)
  AC_MSG_RESULT(no)
  REDIRECT=0
]) 

AC_MSG_CHECKING(whether to discard path_info + path_translated)
AC_ARG_ENABLE(discard_path,
[  --enable-discard-path   If this is enabled, the PHP CGI binary
                          can safely be placed outside of the
                          web tree and people will not be able
                          to circumvent .htaccess security. ],
[
  if test "$enableval" = "yes"; then
    AC_DEFINE(DISCARD_PATH, 1)
    AC_MSG_RESULT(yes)
    DISCARD_PATH=1
  else
    AC_DEFINE(DISCARD_PATH, 0)
    AC_MSG_RESULT(no)
    DISCARD_PATH=0
  fi
],[
  AC_DEFINE(DISCARD_PATH, 0)
  AC_MSG_RESULT(no)
  DISCARD_PATH=0
]) 
fi

AC_MSG_CHECKING(whether to enable a memory limit)
AC_ARG_ENABLE(memory-limit,
[  --enable-memory-limit   Compile with memory limit support. ],
[
  if test "$enableval" = "yes"; then
    AC_DEFINE(MEMORY_LIMIT, 1)
    AC_MSG_RESULT(yes)
  else
    AC_DEFINE(MEMORY_LIMIT, 0)
    AC_MSG_RESULT(no)
  fi
],[
  AC_DEFINE(MEMORY_LIMIT, 0)
  AC_MSG_RESULT(no)
]) 

AC_MSG_CHECKING(whether to enable short tags by default)
AC_ARG_ENABLE(short-tags,
[  --disable-short-tags    Disable the short-form <? start tag by default.],
[
  if test "$enableval" = "no"; then
    AC_DEFINE(DEFAULT_SHORT_OPEN_TAG, 0)
    AC_MSG_RESULT(no)
  else
    AC_DEFINE(DEFAULT_SHORT_OPEN_TAG, 1)
    AC_MSG_RESULT(yes)
  fi
],[
  AC_DEFINE(DEFAULT_SHORT_OPEN_TAG, 1)
  AC_MSG_RESULT(yes)
]) 

AC_MSG_CHECKING(whether to enable the URL-aware fopen wrapper)
AC_ARG_ENABLE(url-fopen-wrapper,
[  --disable-url-fopen-wrapper Disable the URL-aware fopen wrapper that allows
                              accessing files via http or ftp.],
[
  if test "$enableval" = "yes"; then
    AC_DEFINE(PHP3_URL_FOPEN, 1)
    AC_MSG_RESULT(yes)
  else
    AC_DEFINE(PHP3_URL_FOPEN, 0)
    AC_MSG_RESULT(no)
  fi
],[
  AC_DEFINE(PHP3_URL_FOPEN, 1)
  AC_MSG_RESULT(yes)
])


divert(3)

AC_SUBST(EXTRA_LIBS)

# reading config stubs
esyscmd(./scripts/config-stubs)

AC_MSG_CHECKING(for IMAP support)
AC_ARG_WITH(imap,
[  --with-imap[=DIR]       Include IMAP support (DIR is the IMAP include dir and c-client.a dir).],
[
  if test "$withval" != "no"; then
	if test "$withval" = "yes"; then
        IMAP_DIR=/usr/local
	else
		IMAP_DIR=$withval
    fi
    if test ! -f $IMAP_DIR/include/mail.h; then
      AC_MSG_ERROR(could not find mail.h in $IMAP_DIR/include !)
	fi
    if test ! -f  $IMAP_DIR/include/rfc822.h; then
      AC_MSG_ERROR(could not find rfc822.h in $IMAP_DIR/include !)
	fi
    if test ! -f $IMAP_DIR/include/linkage.h; then
      AC_MSG_ERROR(could not find linkage.h in $IMAP_DIR/include !)
	fi
    if test ! -f $IMAP_DIR/lib/c-client.a; then
      AC_MSG_ERROR(could not find c-client.a in $IMAP_DIR/lib !)
	fi
    IMAP_INCLUDE="-I$IMAP_DIR/include/"
    IMAP_LIBS="$IMAP_DIR/lib/c-client.a"

dnl## AC_SUBST(IMAP_INCLUDE)
    INCLUDES="$INCLUDES $IMAP_INCLUDE"
    AC_SUBST(IMAP_LIBS)
    LIBS="$LIBS $IMAP_LIBS"
    AC_DEFINE(HAVE_IMAP)
    AC_MSG_RESULT(yes)
  else
    AC_MSG_ERROR(could not find imap library!)
  fi
],[
    AC_MSG_RESULT(no)
])
  

AC_MSG_CHECKING(for Sybase support)
AC_ARG_WITH(sybase,
[  --with-sybase[=DIR]     Include Sybase-DB support.  DIR is the Sybase home
                          directory, defaults to /home/sybase.],
[
  if test "$withval" != "no"; then
  	if test "$withval" = "yes"; then
		SYBASE_INCDIR=/home/sybase/include
		SYBASE_LIBDIR=/home/sybase/lib
   	else
   		SYBASE_INCDIR=$withval/include
   		SYBASE_LIBDIR=$withval/lib
   	fi
    SYBASE_INCLUDE=-I$SYBASE_INCDIR
    SYBASE_LFLAGS="-L$SYBASE_LIBDIR -L$SYBASE_LIBDIR"
    SYBASE_LIBS=-lsybdb
    AC_MSG_RESULT(yes)
    AC_CHECK_LIB(dnet_stub, dnet_addr,
     [ SYBASE_LIBS="$SYBASE_LIBS -ldnet_stub"
        AC_DEFINE(HAVE_LIBDNET_STUB)
     ])
    AC_DEFINE(HAVE_SYBASE)
  else
  	AC_MSG_RESULT(no)
  fi
],[
  AC_MSG_RESULT(no)
])
AC_SUBST(SYBASE_LIBS)
AC_SUBST(SYBASE_LFLAGS)
dnl## AC_SUBST(SYBASE_INCLUDE)
INCLUDES="$INCLUDES $SYBASE_INCLUDE"

AC_MSG_CHECKING(for Sybase-CT support)
AC_ARG_WITH(sybase-ct,
[  --with-sybase-ct[=DIR]  Include Sybase-CT support.  DIR is the Sybase home
                          directory, defaults to /home/sybase.],
[
  if test "$withval" != "no"; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(HAVE_SYBASE_CT)
  	if test "$withval" = "yes"; then
		SYBASE_CT_INCDIR=/home/sybase/include
		SYBASE_CT_LIBDIR=/home/sybase/lib
   	else
   		SYBASE_CT_INCDIR=$withval/include
   		SYBASE_CT_LIBDIR=$withval/lib
   	fi
    SYBASE_CT_INCLUDE=-I$SYBASE_CT_INCDIR
    SYBASE_CT_LFLAGS="-L$SYBASE_CT_LIBDIR"
    SYBASE_CT_LIBS="-lcs -lct -lcomn -lintl"
    old_LDFLAGS=$LDFLAGS
    LDFLAGS="$LDFLAGS -L$SYBASE_CT_LIBDIR"
    AC_CHECK_LIB(tcl, netg_errstr,
              [ SYBASE_CT_LIBS="$SYBASE_CT_LIBS -ltcl" ],
              [ SYBASE_CT_LIBS="$SYBASE_CT_LIBS -lsybtcl" ],
              [ $SYBASE_CT_LIBS ])
    AC_CHECK_LIB(insck, insck__getVdate,
              [ SYBASE_CT_LIBS="$SYBASE_CT_LIBS -linsck" ])
      LDFLAGS=$old_LDFLAGS
  else
  	AC_MSG_RESULT(no)
  fi
],[
  AC_MSG_RESULT(no)
])
AC_SUBST(SYBASE_CT_LIBS)
AC_SUBST(SYBASE_CT_LFLAGS)
dnl## AC_SUBST(SYBASE_CT_INCLUDE)
INCLUDES="$INCLUDES $SYBASE_CT_INCLUDE"


AC_MSG_CHECKING(for PostgresSQL support)
AC_ARG_WITH(pgsql,
[  --with-pgsql[=DIR]      Include PostgresSQL support.  DIR is the PostgresSQL
                          base install directory, defaults to /usr/local/pgsql.],
[
  if test "$withval" != "no"; then
  	if test "$withval" = "yes"; then
	    PGSQL_INCDIR=/usr/local/pgsql/include
   		PGSQL_LIBDIR=/usr/local/pgsql/lib
   	else
   		PGSQL_INCDIR=$withval/include
   		test -d $withval/include/pgsql && PGSQL_INCDIR=$withval/include/pgsql
   		PGSQL_LIBDIR=$withval/lib
   		test -d $withval/lib/pgsql && PGSQL_LIBDIR=$withval/lib/pgsql
   	fi
    PGSQL_INCLUDE=-I$PGSQL_INCDIR
    PGSQL_LFLAGS=-L$PGSQL_LIBDIR
    PGSQL_LIBS=-lpq

    old_CFLAGS=$CFLAGS; old_LDFLAGS=$LDFLAGS; old_LIBS=$LIBS
    CFLAGS="$CFLAGS $PGSQL_INCLUDE"
    LDFLAGS="$LDFLAGS $PGSQL_LFLAGS"
    LIBS="$LIBS $PGSQL_LIBS"
    AC_DEFINE(HAVE_PGSQL)
    AC_MSG_RESULT(yes)
    AC_CHECK_FUNC(PQcmdTuples,AC_DEFINE(HAVE_PQCMDTUPLES))
    CFLAGS=$old_CFLAGS; LDFLAGS=$old_LDFLAGS; LIBS=$old_LIBS
  else
    AC_MSG_RESULT(no)
  fi
],[
  AC_MSG_RESULT(no)
])
AC_SUBST(PGSQL_LIBS)
AC_SUBST(PGSQL_LFLAGS)
dnl## AC_SUBST(PGSQL_INCLUDE)
INCLUDES="$INCLUDES $PGSQL_INCLUDE"


AC_MSG_CHECKING(for LDAP support)
AC_ARG_WITH(ldap,
[  --with-ldap[=DIR]       Include LDAP support.  DIR is the LDAP base
                          install directory, defaults to /usr/local/ldap],
[
  if test "$withval" != "no"; then
  	if test "$withval" = "yes"; then
	    LDAP_INCDIR=/usr/local/ldap/include
   		LDAP_LIBDIR=/usr/local/ldap/lib
   	else
   		LDAP_INCDIR=$withval/include
   		LDAP_LIBDIR=$withval/lib
   	fi
    LDAP_INCLUDE=-I$LDAP_INCDIR
    LDAP_LFLAGS="-L$LDAP_LIBDIR ${ld_runpath_switch}$LDAP_LIBDIR"
    LDAP_LIBS="-lldap -llber"

    AC_DEFINE(HAVE_LDAP)

    AC_MSG_RESULT(yes)
  else
  	AC_MSG_RESULT(no)
  fi
],[
  AC_MSG_RESULT(no)
])
AC_SUBST(LDAP_LIBS)
AC_SUBST(LDAP_LFLAGS)
dnl## AC_SUBST(LDAP_INCLUDE)
INCLUDES="$INCLUDES $LDAP_INCLUDE"


AC_MSG_CHECKING(for SNMP support)
AC_ARG_WITH(snmp,
[  --with-snmp[=DIR]       Include SNMP support.  DIR is the SNMP base
                          install directory, defaults to /usr/local],
[
  if test "$withval" != "no"; then
  	if test "$withval" = "yes"; then
	   	SNMP_INCDIR=/usr/local/include
   		test -d /usr/local/include/ucd-snmp && SNMP_INCDIR=/usr/local/include/ucd-snmp
   		SNMP_LIBDIR=/usr/local/lib
   	else
   		SNMP_INCDIR=$withval/include
   		test -d $withval/include/ucd-snmp && SNMP_INCDIR=$withval/include/ucd-snmp
   		SNMP_LIBDIR=$withval/lib
   	fi
    SNMP_INCLUDE=-I$SNMP_INCDIR
    SNMP_LFLAGS=-L$SNMP_LIBDIR
    SNMP_LIBS="-lsnmp"

    AC_DEFINE(HAVE_SNMP)

    AC_MSG_RESULT(yes)
  else
  	AC_MSG_RESULT(no)
  fi
],[
  AC_MSG_RESULT(no)
])
AC_SUBST(SNMP_LIBS)
AC_SUBST(SNMP_LFLAGS)
dnl## AC_SUBST(SNMP_INCLUDE)
INCLUDES="$INCLUDES $SNMP_INCLUDE"


AC_MSG_CHECKING(for Informix support)
AC_ARG_WITH(informix,
[  --with-informix[=DIR]   Include Informix support.  DIR is the Informix base
                          install directory, defaults to ${INFORMIXDIR:-nothing}.],
[
	if test "$withval" != "no"; then
		if test "$INFORMIXDIR" = ""; then
			INFORMIX_WARNING="
WARNING: You asked for Informix support, but don't have \\\$INFORMIXDIR
	 environment value set up. Configuring and compiling Informix
	 support to PHP3 is impossible and has been turned off. Please
	 try again after setting up your environment."
			AC_MSG_RESULT(no)
		else
			if test "$withval" = "yes"; then
				IFX_INCDIR=$INFORMIXDIR/incl/esql
				IFX_LIBDIR="-L$INFORMIXDIR/lib -L$INFORMIXDIR/lib/esql"
			else
				IFX_INCDIR=$withval/incl/esql
				IFX_LIBDIR="-L$withval/lib -L$withval/lib/esql"
				if test "$withval" != "$INFORMIXDIR"; then
					INFORMIX_WARNING="
WARNING: You specified Informix base install directory that is different
	 than your \\\$INFORMIXDIR environment variable. You'd better know
	 exactly what you are doing."
				fi
    		fi
			IFX_INCLUDE=-I$IFX_INCDIR
			IFX_LFLAGS=$IFX_LIBDIR
			if test -z "$IFX_LIBS"; then
				IFX_LIBS=`esql -libs | sed -e 's/-lm$//'`
				dnl  -lm twice otherwise?
				IFX_LIBS=`echo $IFX_LIBS | sed -e 's/Libraries to be used://g' -e 's/esql: error -55923: No source or object file\.//g'`
				dnl Seems to get rid of newlines.
				dnl According to Perls DBD-Informix, might contain these strings.
			else
				dnl Allow override to use static and/or threaded libs
				IFX_LIBS="$IFX_LIBS"
			fi
			CFLAGS="$CFLAGS $IFX_INCLUDE"
			LDFLAGS="$LDFLAGS $IFX_LFLAGS"

		    if test "`uname -s 2>/dev/null`" = "AIX"; then
				CFLAGS="$CFLAGS -D__H_LOCALEDEF"
			fi
			AC_DEFINE(HAVE_IFX)
			AC_MSG_CHECKING([Informix version])
			IFX_VERSION=[`esql -V | sed -ne '1 s/^[^0-9]*\([0-9]\)\.\([0-9]*\).*/\1\2/p'`]
			if test $IFX_VERSION -ge "900"; then
				AC_DEFINE(HAVE_IFX_IUS)
				IFX_ESQL_FLAGS="-EDHAVE_IFX_IUS"
			else
				IFX_ESQL_FLAGS="-EUHAVE_IFX_IUS"
			fi
			AC_SUBST(IFX_ESQL_FLAGS)
			AC_DEFINE_UNQUOTED(IFX_VERSION, $IFX_VERSION)
			AC_MSG_RESULT(yes)
		fi
	else
		INFORMIXDIR=
		AC_MSG_RESULT(no)
	fi
],[
  AC_MSG_RESULT(no)
])
AC_SUBST(IFX_LIBS)
AC_SUBST(IFX_LFLAGS)
AC_SUBST(INFORMIXDIR)
dnl## AC_SUBST(IFX_INCLUDE)
INCLUDES="$INCLUDES $IFX_INCLUDE"


AC_MSG_CHECKING(for InterBase support)
AC_ARG_WITH(interbase,
[  --with-interbase[=DIR]  Include InterBase support.  DIR is the InterBase base
                          install directory, defaults to /usr/interbase],
[
  if test "$withval" != "no"; then
  	if test "$withval" = "yes"; then
	   	IBASE_INCDIR=/usr/interbase/include
   		IBASE_LIBDIR=/usr/interbase/lib
   	else
   		IBASE_INCDIR=$withval/include
   		IBASE_LIBDIR=$withval/lib
   	fi
    IBASE_INCLUDE=-I$IBASE_INCDIR
    IBASE_LFLAGS=-L$IBASE_LIBDIR
    IBASE_LIBS="-lgds"

    AC_DEFINE(HAVE_IBASE)

    AC_MSG_RESULT(yes)
  else
  	AC_MSG_RESULT(no)
  fi
],[
  AC_MSG_RESULT(no)
])
AC_SUBST(IBASE_LIBS)
AC_SUBST(IBASE_LFLAGS)
dnl## AC_SUBST(IBASE_INCLUDE)
INCLUDES="$INCLUDES $IBASE_INCLUDE"


AC_MSG_CHECKING(for XML support)
AC_ARG_WITH(xml,
[  --with-xml              Include XML support],[
  if test "$withval" = "yes"; then
    AC_DEFINE(HAVE_LIBEXPAT, 1)
    AC_MSG_RESULT(yes)
    XML_LIBS="-lexpat"
  else
    AC_MSG_RESULT(no)
  fi
],[
  AC_MSG_RESULT(no)
]) 
AC_SUBST(XML_LIBS)


AC_MSG_CHECKING(whether to include zlib support)
AC_ARG_WITH(zlib,
[  --with-zlib[=DIR]       Include zlib support (requires zlib >= 1.0.9).
                          DIR is the zlib install directory,
                          defaults to /usr.],
[
  case "$withval" in
    no)
      AC_MSG_RESULT(no) ;;
    yes)
      AC_MSG_RESULT(yes)
      AC_CHECK_LIB(z, gzgets, [AC_DEFINE(HAVE_ZLIB) ZLIB_LIBS="-lz"],
        [AC_MSG_ERROR(Zlib module requires zlib >= 1.0.9.)]) ;;
    *)
      test -f $withval/include/zlib/zlib.h && ZLIB_INCLUDE="-I$withval/include/zlib"
      test -f $withval/include/zlib.h && ZLIB_INCLUDE="-I$withval/include"
      if test -n "$ZLIB_INCLUDE" ; then
        AC_MSG_RESULT(yes)
        old_LIBS=$LIBS
        LIBS="$LIBS -L$withval/lib"
        AC_CHECK_LIB(z, gzgets, [AC_DEFINE(HAVE_ZLIB) ZLIB_LIBS="-L$withval/lib -lz"],
          [AC_MSG_ERROR(Zlib module requires zlib >= 1.0.9.)])
        LIBS=$old_LIBS
      else
        AC_MSG_RESULT(no)
      fi ;;
  esac
],[
  AC_MSG_RESULT(no)
])
AC_SUBST(ZLIB_LIBS)
dnl## AC_SUBST(ZLIB_INCLUDE)
INCLUDES="$INCLUDES $ZLIB_INCLUDE"


AC_MSG_CHECKING(whether to include pdflib support)
AC_ARG_WITH(pdflib,
[  --with-pdflib[=DIR]     Include pdflib support (tested with 0.6).
                          DIR is the pdflib install directory,
                          defaults to /usr/local.],
[
  case "$withval" in
    no)
      AC_MSG_RESULT(no) ;;
    yes)
      AC_MSG_RESULT(yes)
      AC_CHECK_LIB(pdf, PDF_open, [AC_DEFINE(HAVE_PDFLIB) PDFLIB_LIBS="-lpdf"],
        [AC_MSG_ERROR(pdflib module requires pdflib 0.6)]) ;;
    *)
      test -f $withval/include/pdf.h && PDFLIB_INCLUDE="-I$withval/include"
      if test -n "$PDFLIB_INCLUDE" ; then
        AC_MSG_RESULT(yes)
        old_LIBS=$LIBS
        LIBS="$LIBS -L$withval/lib"
        AC_CHECK_LIB(pdf, PDF_open, [AC_DEFINE(HAVE_PDFLIB) PDFLIB_LIBS="-L$withval/lib -lpdf"],
          [AC_MSG_ERROR(pdflib module requires pdflib 0.6.)])
        LIBS=$old_LIBS
      else
        AC_MSG_RESULT(no)
      fi ;;
  esac
],[
  AC_MSG_RESULT(no)
])
AC_SUBST(PDFLIB_LIBS)
dnl## AC_SUBST(PDFLIB_INCLUDE)
INCLUDES="$INCLUDES $PDFLIB_INCLUDE"


AC_MSG_CHECKING(whether to include fdftk support)
AC_ARG_WITH(fdftk,
[  --with-fdftk[=DIR]      Include fdftk support.
                          DIR is the fdftk lib install directory,
                          defaults to /usr/local.],
[
  case "$withval" in
    no)
      AC_MSG_RESULT(no) ;;
    yes)
      AC_MSG_RESULT(yes)
      AC_CHECK_LIB(FdfTk, FDFOpen, [AC_DEFINE(HAVE_FDFLIB) FDFLIB_LIBS="-lFdfTk"],
        [AC_MSG_ERROR(fdftk module requires fdftk 2.0)]) ;;
    *)
      test -f $withval/include/Fdftk.h && FDFLIB_INCLUDE="-I$withval/include"
      if test -n "$FDFLIB_INCLUDE" ; then
        AC_MSG_RESULT(yes)
        old_LIBS=$LIBS
        LIBS="$LIBS -L$withval/lib"
        AC_CHECK_LIB(FdfTk, FDFOpen, [AC_DEFINE(HAVE_FDFLIB) FDFLIB_LIBS="-L$withval/lib -lFdfTk"],
          [AC_MSG_ERROR(fdftk module requires ftftk lib 2.0.)])
        LIBS=$old_LIBS
      else
        AC_MSG_RESULT(no)
      fi ;;
  esac
],[
  AC_MSG_RESULT(no)
])
AC_SUBST(FDFLIB_LIBS)
dnl## AC_SUBST(FDFLIB_INCLUDE)
INCLUDES="$INCLUDES $FDFLIB_INCLUDE"


AC_MSG_CHECKING(whether to enable System V semaphore support)
AC_ARG_ENABLE(sysvsem,
[  --enable-sysvsem        Enable System V semaphore support.],
[
  if test "$enableval" = "yes"; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(HAVE_SYSVSEM, 1)
    AC_CACHE_CHECK(for union semun,php_cv_semun,
	    AC_TRY_COMPILE([
#include <sys/types.h>
#include <sys/ipc.h>
#include <sys/sem.h>
			],
			[union semun x;],
			[
				php_cv_semun=yes
			],[
				php_cv_semun=no
			])
		)
	if test $php_cv_semun = "yes"; then
		AC_DEFINE(HAVE_SEMUN, 1)
	else
		AC_DEFINE(HAVE_SEMUN, 0)
	fi
  else
    AC_MSG_RESULT(no)
    AC_DEFINE(HAVE_SYSVSEM, 0)
  fi
],[
  AC_MSG_RESULT(no)
  AC_DEFINE(HAVE_SYSVSEM, 0)
])


AC_MSG_CHECKING(whether to enable System V shared memory support)
AC_ARG_ENABLE(sysvshm,
[  --enable-sysvshm        Enable the System V shared memory support],[
  if test "$enableval" = "yes"; then
    AC_DEFINE(HAVE_SYSVSHM, 1)
    AC_MSG_RESULT(yes)
  else
    AC_DEFINE(HAVE_SYSVSHM, 0)
    AC_MSG_RESULT(no)
  fi
],[
  AC_DEFINE(HAVE_SYSVSHM, 0)
  AC_MSG_RESULT(no)
]) 


divert(4)

dnl If we're using gcc and the user hasn't specified CFLAGS, add -O2.
test -n "$auto_cflags" && test -n "$GCC" && CFLAGS="$CFLAGS -O2"

dnl If we're using cc on HP-UX, add the -Ae to CFLAGS
if test -n "$auto_cflags" && test "`uname -s 2>/dev/null`" = "HP-UX"; then
   test -n "$GCC" || CFLAGS="-Ae $CFLAGS"
fi

dnl *** Commented out - generates slow code and consumes a lot of 
dnl *** resources during compilation - we need to figure out how
dnl *** to supply it only when absolutely necessary
dnl If we are using gcc add -fpic to make dl() work on some platforms
dnl test -n "$GCC" && CFLAGS="$CFLAGS -fpic"

AC_SUBST(CFLAGS)
AC_SUBST(PROG_SENDMAIL)
AC_SUBST(CFLAGS_SHLIB)
AC_SUBST(LDFLAGS_SHLIB)
AC_SUBST(LDFLAGS_SHLIB_EXPORT)

dnl Create the necessary directories if building using VPATH
if test ! -d functions; then
	mkdir functions
fi

PHP_BUILD_DATE=`date '+%Y-%m-%d'`
AC_SUBST(PHP_BUILD_DATE)
AC_DEFINE_UNQUOTED(PHP_BUILD_DATE,"$PHP_BUILD_DATE")
PHP_UNAME=`uname -a`
AC_DEFINE_UNQUOTED(PHP_UNAME,"$PHP_UNAME")
PHP_OS=`uname`
AC_DEFINE_UNQUOTED(PHP_OS,"$PHP_OS")

AC_OUTPUT(Makefile build-defs.h libphp3.module
          scripts/mkextlib regex/Makefile ext/Makefile
          @@EXT_MAKEFILES@@, [
chmod +x scripts/mkextlib
], [

dnl Warn about CGI version with no extra security options.
if test "$BINNAME" = "php"; then
	if test "$REDIRECT" = "0"; then
		if test "$DISCARD_PATH" = "0"; then
		echo "WARNING: You will be compiling the CGI version of PHP without any"
		echo "         redirection checking.  By putting this cgi binary somewhere"
		echo "         in your web space, users may be able to circumvent existing .htaccess"
		echo "         security by loading files directly through the parser.  See"
		echo "         http://www.php.net/manual/config-security.php3 for more details."
		fi
	fi
fi
dnl Warn if Informix support was requested but environment is not set up correctly.
if test "$INFORMIX_WARNING" != ""; then
		echo "$INFORMIX_WARNING"
fi
])

divert

# Local Variables:
# tab-width: 4
# End:
